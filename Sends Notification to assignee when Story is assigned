import com.atlassian.jira.component.ComponentAccessor
import com.atlassian.jira.issue.Issue
import com.atlassian.mail.Email
import com.atlassian.mail.server.SMTPMailServer
import com.atlassian.jira.user.ApplicationUser
import com.atlassian.jira.event.issue.IssueEvent


def issue = event.issue as Issue
def projectKey = "CHIF"
def issueTypeName = "Story"

if (issue.projectObject.key != projectKey || issue.issueType?.name != issueTypeName) {
    log.debug("[StoryNotification] Skipped: ${issue.key} not matching ${projectKey}/${issueTypeName}")
    return
}

def assignee = issue.assignee
if (!assignee) {
    log.info("[StoryNotification] No assignee for ${issue.key}, skipping email.")
    return
}

def recipientEmail = assignee.emailAddress
if (!recipientEmail) {
    log.warn("[StoryNotification] Assignee ${assignee.name} for ${issue.key} has no email address configured.")
    return
}

def baseUrl = ComponentAccessor.applicationProperties.getString("jira.baseurl")
def subject = "New Story Assigned: ${issue.key}"
def body = """
<p>Hello ${assignee.displayName},</p>

<p>A new <b>Story</b> has been assigned to you in the '<b>${projectKey}</b>' project.</p>

<ul>
  <li><b>Summary:</b> ${issue.summary}</li>
  <li><b>Priority:</b> ${issue.priority?.name ?: 'N/A'}</li>
</ul>

<p><a href="${baseUrl}/browse/${issue.key}">View in Jira</a></p>

<p>Please review it at your earliest convenience.</p>

<p>Kind regards,<br>
Jira Team </p>
"""

sendEmail(recipientEmail, subject, body, issue.key)

def sendEmail(String to, String subject, String body, String issueKey) {
    def mailServer = ComponentAccessor.mailServerManager.defaultSMTPMailServer
    if (mailServer) {
        def email = new Email(to)
        email.setSubject(subject)
        email.setMimeType("text/html")
        email.setBody(body)
        try {
            mailServer.send(email)
            log.info("[StoryNotification] Email sent to ${to} for ${issueKey}")
        } catch (Exception e) {
            log.error("[StoryNotification] Failed to send email to ${to} for ${issueKey}", e)
        }
    } else {
        log.error("[StoryNotification] No default SMTP mail server configured in Jira.")
    }
}
